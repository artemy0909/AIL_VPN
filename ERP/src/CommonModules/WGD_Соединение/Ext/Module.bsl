
#Область ОбщиеМетоды

Функция СтандартныеЗаголовокиДляWGD(API_Ключ)
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json"); 
	Заголовки.Вставить("wg-dashboard-apikey", API_Ключ);
	Возврат Заголовки;
	
КонецФункции


Функция СформироватьЗапросWGD(СерверСсылка, Метод, API_URL, ПараметрыЗапроса=Неопределено) Экспорт
	
	Сервер = СерверСсылка.ПолучитьОбъект();
	
	Возврат HTTP_Соединение.ЗапросHTTP(
		Сервер.WGD_Домен, Сервер.WGD_Порт, "/api" + API_URL,
		Метод, СтандартныеЗаголовокиДляWGD(Сервер.WGD_API_Ключ),
		ПараметрыЗапроса, Сервер.WGD_TLS
	);
	
КонецФункции

	
#КонецОбласти


#Область ВзаимодействиеAPI

Функция ПолучитьСтатусКонфигурации(ИмяКонфигурации, СерверСсылка, ПервыйКонтакт=Ложь) Экспорт
	
	Попытка
		ПараметрыОтвета = СформироватьЗапросWGD(СерверСсылка, "GET", "/getWireguardConfigurationInfo?configurationName=" + ИмяКонфигурации);
	Исключение
		Возврат Перечисления.СтатусКонфигурации.ОшибкаЗапроса;
	КонецПопытки;
	
	Если ПараметрыОтвета.status = Ложь Тогда
		
		Возврат Перечисления.СтатусКонфигурации.НеСуществует;	
		
	ИначеЕсли ПараметрыОтвета.data.configurationInfo.status = Ложь Тогда
		
		Возврат Перечисления.СтатусКонфигурации.Выключена;
		
	ИначеЕсли ПервыйКонтакт И ПараметрыОтвета.data.configurationInfo.TotalPeers <> 0 Тогда
		
		Возврат Перечисления.СтатусКонфигурации.НеПустаяПриДобавлении;
				
	Иначе
		
		Возврат Перечисления.СтатусКонфигурации.Ок;
		
	КонецЕсли;
	
КонецФункции
	
#КонецОбласти