
#Область ОбщиеМетоды

Функция СтандартныеЗаголовокиДляWGD(API_Ключ)
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json"); 
	Заголовки.Вставить("wg-dashboard-apikey", API_Ключ);
	Возврат Заголовки;
	
КонецФункции


Функция СформироватьЗапросWGD(СерверСсылка, Метод, API_URL, ПараметрыЗапроса=Неопределено, ТаймаутСекунды=30)
	
	Сервер = СерверСсылка.ПолучитьОбъект();
	
	Возврат HTTP_Соединение.ЗапросHTTP(
		Сервер.WGD_Домен, Сервер.WGD_Порт, "/api" + API_URL,
		Метод, СтандартныеЗаголовокиДляWGD(Сервер.WGD_API_Ключ),
		ПараметрыЗапроса, Сервер.WGD_TLS, ТаймаутСекунды
	);
	
КонецФункции

	
#КонецОбласти

#Область ВзаимодействиеAPI

Функция ПолучитьДанныеКонфигурации(ИмяКонфигурации, СерверСсылка, ПервыйКонтакт=Ложь) Экспорт
	
	Попытка
		ПараметрыОтвета = СформироватьЗапросWGD(СерверСсылка, "GET", "/getWireguardConfigurationInfo?configurationName=" + ИмяКонфигурации);
	Исключение
		Возврат Перечисления.СтатусКонфигурации.ОшибкаЗапроса;
	КонецПопытки;
		
	Если ПараметрыОтвета.status = Ложь Тогда
		
		Возврат Перечисления.СтатусКонфигурации.НеСуществует;	
		
	ИначеЕсли ПараметрыОтвета.data.configurationInfo.status = Ложь Тогда
		
		Возврат Перечисления.СтатусКонфигурации.Выключена;
		
	ИначеЕсли ПервыйКонтакт И ПараметрыОтвета.data.configurationInfo.TotalPeers <> 0 Тогда
		
		Возврат Перечисления.СтатусКонфигурации.НеПустаяПриДобавлении;
				
	Иначе
		
		Возврат Перечисления.СтатусКонфигурации.Ок;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьВсеСлотыКонфигурации(СерверСсылка, ИмяКонфигурации) Экспорт
	
	Возврат СформироватьЗапросWGD(СерверСсылка, "GET", "/downloadAllPeers/" + ИмяКонфигурации);
	
КонецФункции

Функция ПолучитьСтатусыКонфигурацийСервера(СерверСсылка) Экспорт
	
	Возврат СформироватьЗапросWGD(СерверСсылка, "GET", "/getWireguardConfigurations",,3);
	
КонецФункции

Функция ГенерироватьСлотыКонфигурации(ИмяКонфигурации, СерверСсылка, Количество) Экспорт
	
	ПараметрыЗапроса = Новый Структура(
		"bulkAdd,bulkAddAmount,preshared_key_bulkAdd,advanced_security",
		Истина, Количество, Истина, "on"
	);
	
	Возврат СформироватьЗапросWGD(
		СерверСсылка,
		"POST",
		"/addPeers/" + ИмяКонфигурации,
		ПараметрыЗапроса
	);	
	
КонецФункции

Функция ПолучитьДанныеСлотаКонфигурации(ИмяКонфигурации, СерверСсылка, ИдСлота) Экспорт
	
	API_URL = СтрШаблон("/downloadPeer/%1?id=%2", ИмяКонфигурации, ИдСлота);

	Возврат СформироватьЗапросWGD(
		СерверСсылка, "GET", API_URL
	);

КонецФункции

Функция УдалитьСлоты(ИмяКонфигурации, СерверСсылка, СлотыИд) Экспорт
	
	ПарметрыЗапроса = Новый Структура("peers", СлотыИд);
	
	Возврат СформироватьЗапросWGD(
		СерверСсылка, "POST", "/deletePeers/" + ИмяКонфигурации, ПарметрыЗапроса
	);
	
КонецФункции

#КонецОбласти