
#Область ОбщиеМетоды

Функция ПолучитьПараметрыОтвета(Ответ) Экспорт
	
	Попытка
		
		Возврат ОбщегоНазначения.JSONВСтруктуру(Ответ.ПолучитьТелоКакСтроку());
	
	Исключение
		
		ВызватьИсключение СтрШаблон("Не удалось прочитать ответ API (%1)", ОписаниеОшибки());
		
	КонецПопытки;
	
КонецФункции

Функция СформироватьОтвет(ПараметрыОтвета, КодОтвета=200) Экспорт
	
	Ответ = Новый HTTPСервисОтвет(КодОтвета);
	Ответ.УстановитьТелоИзСтроки(ОбщегоНазначения.ОбъектВJSON(ПараметрыОтвета));
	Возврат Ответ;
	
КонецФункции

Функция ЗапросHTTP(Домен, Порт, Относительный_URL, Метод, Заголовки, ПараметрыЗапроса=Неопределено, TLS=Ложь) Экспорт
	
	Попытка
		
		ЗащищенноеСоединение = Неопределено;
		Если TLS Тогда
			ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(
	            Новый СертификатКлиентаОС,
	            Новый СертификатыУдостоверяющихЦентровОС,
	            РежимПроверкиОтзываTLSСертификатаСервера.Строгий
			);	
		КонецЕсли;
		
		Соединение = Новый HTTPСоединение(
	        Домен, Число(Порт),,,,,
			ЗащищенноеСоединение
	    );
                                         
	    Запрос = Новый HTTPЗапрос(Относительный_URL, Заголовки);
		Запрос.УстановитьТелоИзСтроки(ОбщегоНазначения.ОбъектВJSON(ПараметрыЗапроса));
		Ответ = Соединение.ВызватьHTTPМетод(Метод, Запрос);
		
		Возврат ПолучитьПараметрыОтвета(Ответ);
	
	Исключение
		
		ВызватьИсключение СтрШаблон("Прозошла непредвиденная ошибка при выполнении запроса (%1)", ОписаниеОшибки());
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

