Функция Процент()

	Возврат "%";
	
КонецФункции

Функция Рубль()

	Возврат "₽";
	
КонецФункции

Функция ПрайсЛистПоПромокоду(ДанныеПромокода, ПромокодСтрока) Экспорт
		
	Если ТипЗнч(ДанныеПромокода.ИсточникПодписки) = Тип("ДокументСсылка.ПерсональныйПромокод") Тогда
		Тип = "personal";	
	ИначеЕсли ТипЗнч(ДанныеПромокода.ИсточникПодписки) = Тип("ДокументСсылка.ПартнерскаяКомпания") Тогда
		Тип = "partner";
	Иначе
		ВызватьИсключение "Необходимо установить представление для данного документа!";
	КонецЕсли;
	ИсточникРегистратор = Новый Структура("type,guid", Тип, ОбщегоНазначения.ПолучитьУИД(ДанныеПромокода.ИсточникПодписки));
	ИнформацияОРеферале = Новый Структура("promo_code,refer_guid,record", ПромокодСтрока, ОбщегоНазначения.ПолучитьУИД(ДанныеПромокода.Реферал), ИсточникРегистратор);
	
	Результат = ПрайсЛист(ДанныеПромокода.ПрайсЛист, ИнформацияОРеферале);
	
	Возврат Результат;
	
КонецФункции

Функция ПрайсЛист(ПрайсЛистОбъект, ИнформацияОРеферале=Неопределено) Экспорт
	
	ЭлементыПрайсЛиста = Новый Массив;
	
	Для Каждого ТарифСтрока Из ПрайсЛистОбъект.Тарифы Цикл
		
		ЭлементыПрайсЛиста.Добавить(Новый Структура("title,tariff_guid,price,discount,amount,source_info",
			ТарифСтрока.Тариф.НаименованиеДляБота, ОбщегоНазначения.ПолучитьУИД(ТарифСтрока.Тариф), ЕдиницаИзмерения(ТарифСтрока.Стоимость, Рубль()),
			ЕдиницаИзмерения(ТарифСтрока.Скидка, Процент()), ЕдиницаИзмерения(ТарифСтрока.Сумма, Рубль()), ИнформацияОРеферале));	
		
	КонецЦикла;		
	
	Возврат Новый Структура("price_items", ЭлементыПрайсЛиста);
	
КонецФункции

Функция ЕдиницаИзмерения(Значение, СимволИзмерения, Разряды=2)
	
	Возврат Новый Структура("value,unit_symbol,semicolons,currency", Значение * Pow(10, Разряды), СимволИзмерения, Разряды, "RUB");	
	
КонецФункции