
Функция ПолучитьКонтрагентаСессииИлиСоздать(ТелеграмИД, Бот, ПолноеИмяТелеграм="Клиент") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СессииТелеграм.Контрагент.Ссылка КАК КонтрагентСсылка
		|ИЗ
		|	РегистрСведений.СессииТелеграм КАК СессииТелеграм
		|ГДЕ
		|	СессииТелеграм.ТелеграмИД = &ТелеграмИД
		|	И СессииТелеграм.Бот = &Бот";
	
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.УстановитьПараметр("ТелеграмИД", ТелеграмИД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		СейчасДата = ТекущаяДата();
	
		НовыйКонтрагент = Справочники.Контрагент.СоздатьЭлемент();
		НовыйКонтрагент.Родитель = ПредопределенноеЗначение("Справочник.Контрагент.Клиенты");
		НовыйКонтрагент.Наименование = СтрШаблон("%1 (%2)", ПолноеИмяТелеграм, ТелеграмИД);
		НовыйКонтрагент.СекретныйКлюч = Строка(Новый УникальныйИдентификатор);
		НовыйКонтрагент.Записать();
		
		НоваяСессия = РегистрыСведений.СессииТелеграм.СоздатьМенеджерЗаписи();
		НоваяСессия.ТелеграмИД = ТелеграмИД;
		НоваяСессия.Бот = Бот;
		НоваяСессия.НачалоСессии = СейчасДата;
		НоваяСессия.Контрагент = НовыйКонтрагент.Ссылка;
		НоваяСессия.Записать(Истина);
		
		Возврат Новый Структура("Контрагент,Создано", НовыйКонтрагент.Ссылка, Истина);
		
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат Новый Структура("Контрагент,Создано", ВыборкаДетальныеЗаписи.КонтрагентСсылка, Ложь);
	
КонецФункции

